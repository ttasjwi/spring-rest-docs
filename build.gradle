plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'

	// Asciidoctor 플러그인 적용
	// gradle 7.0 이상부터는 jvm 사용
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.ttasjwi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	// dependencies에서 설정한 asciidoctorExtensions 를 구성에 추가
	asciidoctorExtensions
}

repositories {
	mavenCentral()
}

ext {
	// asciidoctor 및 mockmvc 버전
	asciidocVersion = "3.0.0"

	// snippet 의 디렉토리 설정(ascii doc 조각을 보관하는 경로)
	snippetsDir = file('build/generated-snippets')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor:${asciidocVersion}"

	// restdocs-mockmvc의 testCompile 구성 -> mockMvc를 사용해서 snippets 조각들을 뽑아낼 수 있게 된다.
	// MockMvc 대신 WebTestClient을 사용하려면 spring-restdocs-webtestclient 추가
	// MockMvc 대신 REST Assured를 사용하려면 spring-restdocs-restassured 를 추가
	testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${asciidocVersion}"
}



test {
	// 위에서 작성한 snippetsDir 디렉토리를 test의 output으로 구성하는 설정 -> 스니펫 조각들이 build/generated-snippets로 출력
	outputs.dir snippetsDir
	useJUnitPlatform()

}

asciidoctor.doFirst {
	// static/docs 폴더 비우기
	delete file('src/main/resources/static/docs')
}

asciidoctor {

	dependsOn test // test 작업 이후에 작동하도록 하는 설정
	configurations 'asciidoctorExtensions' // 위에서 작성한 configuration 적용
	inputs.dir snippetsDir // snippetsDir 를 입력으로 구성

	// JDK 17 기준 asciidoctor Task에서 다음과 같은 경고가 발생한다.
	// WARN FilenoUtil : Native subprocess control requires open access to the JDK IO subsystem
	//	Pass '--add-opens java.base/sun.nio.ch=ALL-UNNAMED --add-opens java.base/java.io=ALL-UNNAMED' to enable.
	// -> 해결법 : https://github.com/asciidoctor/asciidoctor-gradle-plugin/issues/597
	inProcess = org.asciidoctor.gradle.base.process.ProcessMode.JAVA_EXEC
	forkOptions {
		jvmArgs("--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED")
	}
}

// asciidoctor 작업 이후 생성된 HTML 파일을 static/docs 로 copy
task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

build {
	// build 의 의존작업 명시
	dependsOn copyDocument

}
